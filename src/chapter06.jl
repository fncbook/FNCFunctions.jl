# begin euler
"""
    euler(ivp, n)

Apply Euler's method to solve the given IVP using `n` time steps.
Returns a vector of times and a vector of solution values.
"""
function euler(ivp, n)
    # Time discretization.
    a, b = ivp.tspan
    h = (b - a) / n
    t = [a + i * h for i in 0:n]

    # Initial condition and output setup.
    u = fill(float(ivp.u0), n + 1)

    # The time stepping iteration.
    for i in 1:n
        u[i+1] = u[i] + h * ivp.f(u[i], ivp.p, t[i])
    end
    return t, u
end
# end euler

# begin ie2
"""
    ie2(ivp, n)

Apply the Improved Euler method to solve the given IVP using `n`
time steps. Returns a vector of times and a vector of solution
values.
"""
function ie2(ivp, n)
    # Time discretization.
    a, b = ivp.tspan
    h = (b - a) / n
    t = [a + i * h for i in 0:n]

    # Initialize output.
    u = fill(float(ivp.u0), n + 1)

    # Time stepping.
    for i in 1:n
        uhalf = u[i] + h / 2 * ivp.f(u[i], ivp.p, t[i])
        u[i+1] = u[i] + h * ivp.f(uhalf, ivp.p, t[i] + h / 2)
    end
    return t, u
end
# end ie2

# begin rk4
"""
    rk4(ivp, n)

Apply the common Runge-Kutta 4th order method to solve the given
IVP using `n` time steps. Returns a vector of times and a vector of
solution values.
"""
function rk4(ivp, n)

    # Time discretization.
    a, b = ivp.tspan
    h = (b - a) / n
    t = [a + i * h for i in 0:n]

    # Initialize output.
    u = fill(float(ivp.u0), n + 1)

    # Time stepping.
    for i in 1:n
        k₁ = h * ivp.f(u[i], ivp.p, t[i])
        k₂ = h * ivp.f(u[i] + k₁ / 2, ivp.p, t[i] + h / 2)
        k₃ = h * ivp.f(u[i] + k₂ / 2, ivp.p, t[i] + h / 2)
        k₄ = h * ivp.f(u[i] + k₃, ivp.p, t[i] + h)
        u[i+1] = u[i] + (k₁ + 2(k₂ + k₃) + k₄) / 6
    end
    return t, u
end
# end rk4

# begin rk23
"""
    rk23(ivp, tol)

Apply an adaptive embedded RK formula pair to solve given IVP with
estimated error `tol`. Returns a vector of times and a vector of
solution values.
"""
function rk23(ivp, tol)
    # Initialize for the first time step.
    a, b = ivp.tspan
    t = [a]
    u = [float(ivp.u0)]
    i = 1
    h = 0.5 * tol^(1 / 3)
    s₁ = ivp.f(ivp.u0, ivp.p, a)

    # Time stepping.
    while t[i] < b
        # Detect underflow of the step size.
        if t[i] + h == t[i]
            @warn "Stepsize too small near t=$(t[i])"
            break  # quit time stepping loop
        end

        # New RK stages.
        s₂ = ivp.f(u[i] + (h / 2) * s₁, ivp.p, t[i] + h / 2)
        s₃ = ivp.f(u[i] + (3h / 4) * s₂, ivp.p, t[i] + 3h / 4)
        unew3 = u[i] + h * (2s₁ + 3s₂ + 4s₃) / 9   # 3rd order solution
        s₄ = ivp.f(unew3, ivp.p, t[i] + h)
        err = h * (-5s₁ / 72 + s₂ / 12 + s₃ / 9 - s₄ / 8)  # 2nd/3rd difference
        E = norm(err, Inf)                         # error estimate
        maxerr = tol * (1 + norm(u[i], Inf))     # relative/absolute blend

        # Accept the proposed step?
        if E < maxerr     # yes
            push!(t, t[i] + h)
            push!(u, unew3)
            i += 1
            s₁ = s₄       # use FSAL property
        end

        # Adjust step size.
        q = 0.8 * (maxerr / E)^(1 / 3)   # conservative optimal step factor
        q = min(q, 4)               # limit stepsize growth
        h = min(q * h, b - t[i])        # don't step past the end
    end
    return t, u
end
# end rk23

# begin ab4
"""
    ab4(ivp, n)

Apply the Adams-Bashforth 4th order method to solve the given IVP
using `n` time steps. Returns a vector of times and a vector of
solution values.
"""
function ab4(ivp, n)
    # Time discretization.
    a, b = ivp.tspan
    h = (b - a) / n
    t = [a + i * h for i in 0:n]

    # Constants in the AB4 method.
    k = 4
    σ = [55, -59, 37, -9] / 24

    # Find starting values by RK4.
    u = fill(float(ivp.u0), n + 1)
    rkivp = ODEProblem(ivp.f, ivp.u0, (a, a + (k - 1) * h), ivp.p)
    ts, us = rk4(rkivp, k - 1)
    u[1:k] .= us

    # Compute history of u' values, from newest to oldest.
    f = [ivp.f(u[k-i], ivp.p, t[k-i]) for i in 1:k-1]

    # Time stepping.
    for i in k:n
        f = [ivp.f(u[i], ivp.p, t[i]), f[1:k-1]...]   # new value of du/dt
        u[i+1] = u[i] + h * sum(f[j] * σ[j] for j in 1:k)  # advance a step
    end
    return t, u
end
# end ab4

# begin am2
"""
    am2(ivp, n)

Apply the Adams-Moulton 2nd order method to solve given IVP using
`n` time steps. Returns a vector of times and a vector of
solution values.
"""
function am2(ivp, n)
    # Time discretization.
    a, b = ivp.tspan
    h = (b - a) / n
    t = [a + i * h for i in 0:n]

    # Initialize output.
    u = fill(float(ivp.u0), n + 1)

    # Time stepping.
    for i in 1:n
        # Data that does not depend on the new value.
        known = u[i] + h / 2 * ivp.f(u[i], ivp.p, t[i])
        # Find a root for the new value.
        g = z -> z - h / 2 * ivp.f(z, ivp.p, t[i+1]) - known
        unew = levenberg(g, known)
        u[i+1] = unew[end]
    end
    return t, u
end
